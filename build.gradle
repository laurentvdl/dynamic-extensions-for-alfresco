allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'maven'

    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourceJar
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: 'file://' + (project.hasProperty('uploadPath') ? uploadPath : System.properties['java.io.tmpdir']))
            }
        }
    }

    [compileJava, compileTestJava,javadoc]*.options*.encoding = 'UTF-8'

    compileJava {
        sourceCompatibility = 1.6
        targetCompatibility = 1.6
    }

    ext {
        alfrescoVersion = '4.2.e'
        springVersion = '3.0.5.RELEASE'
        geminiVersion = '1.0.2.RELEASE'
        slf4jVersion = '1.7.5'
        osgiVersion = '4.3.1'
        springSurfVersion = '1.2.0-SNAPSHOT'
        junitVersion = '4.8.2'
        transactionApiVersion = '1.1'
        felixVersion = '4.2.1'
        springOsgiUtilVersion = '2.0.5.RELEASE'
        mockitoVersion = '1.9.5'
        jibxVersion = '1.2.3'
        hibernateVersion = '3.2.6.ga'
        servletVersion = '2.5'
    }

    repositories {
        mavenCentral()
        maven {
            url "https://artifacts.alfresco.com/nexus/content/groups/public/"
        }
        maven {
            url "https://artifacts.alfresco.com/nexus/content/groups/public-snapshots/"
        }
    }

    group = 'com.github.dynamicextensionsalfresco'
    version = '1.0.0-SNAPSHOT'

    configurations.all {
        exclude group: 'jaxen'
        resolutionStrategy {
            eachDependency { details ->
                if (details.requested.group == 'org.springframework') {
                    details.useVersion project.ext.springVersion
                }
            }
        }
    }

    configurations {
        amp
        compile.extendsFrom(amp)
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}

defaultTasks 'build'
